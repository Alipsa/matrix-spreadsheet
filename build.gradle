plugins {
    id 'groovy'
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id('io.github.gradle-nexus.publish-plugin') version '1.3.0'
    id "com.github.ben-manes.versions" version "0.46.0"
}

group 'se.alipsa.groovy'
version '1.0-SNAPSHOT'
description 'Groovy spreadsheet import/export'

repositories {
    //mavenLocal()
    mavenCentral()
}

dependencies {
    def groovy = 'org.apache.groovy:groovy:4.0.10'
    compileOnly groovy
    api('se.alipsa.groovy:matrix:1.0.1')
    implementation('org.apache.poi:poi:5.2.3')
    implementation('org.apache.poi:poi-ooxml:5.2.3')

    testImplementation 'org.apache.logging.log4j:log4j-core:2.20.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation groovy
}

test {
    useJUnitPlatform()
}

//Maven Central uploads
task javadocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from groovydoc.destinationDir
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact(javadocJar)
            artifact(sourcesJar)
            pom {
                name = 'Groovy spreadsheet import/export'
                description = "${project.description}"
                url = "https://github.com/Alipsa/spreadsheet"
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/Alipsa/spreadsheet/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'perNyfelt'
                        name = 'Per Nyfelt'
                    }
                }
                scm {
                    url = 'https://github.com/Alipsa/spreadsheet/tree/master'
                    connection = 'scm:git:https://github.com/Alipsa/spreadsheet.git'
                    developerConnection = 'scm:git:https://github.com/Alipsa/spreadsheet.git'
                }
            }
        }
    }
}

signing {
    if (project.properties['signing.keyId'] != null) {
        project.logger.lifecycle("Signing artifacts...")
        sign publishing.publications.maven
    } else {
        project.logger.lifecycle("signing.keyId is not defined, skipping signing of artifacts...")
    }
}